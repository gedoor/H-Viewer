apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

ext {
    VERSION_DATE = new Date().format("yy-MM-dd", TimeZone.getTimeZone("UTC"))
}

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def gitTag = 'git tag'.execute([], project.rootDir).text.trim().split("\n").last()
def gitCommits = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

android {
    signingConfigs {
    }
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "ml.puredark.hviewer"

        minSdkVersion 17
        targetSdkVersion 25

        versionCode 20000 + gitCommits
        versionName gitTag

        resValue "string", "app_name", "H-Viewer"

        // Versioning
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        println "Building ${gitTag} (rev. ${gitCommits} on ${gitSha})"

        // FIR
        buildConfigField "boolean", "FIR_ENABLED", "!DEBUG && ${project.hasProperty('FIR_API_TOKEN')}"
        buildConfigField "String", "FIR_API_TOKEN",
                project.hasProperty('FIR_API_TOKEN') ? "\"${FIR_API_TOKEN}\"" : "null"

        buildConfigField "boolean", "FIR_OTA_ENABLED", "FIR_ENABLED"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            resValue "string", "app_name", "H-Viewer(debug)"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard-fresco.pro'
            zipAlignEnabled true
        }
    }
    lintOptions {
        abortOnError false
    }
    productFlavors {
        github {
            manifestPlaceholders = [UMENG_CHANNEL: "GitHub"]
        }

    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            if (output.outputFile != null && output.outputFile.name.endsWith('.apk')) {
                output.outputFile = file("${output.outputFile.parent}/H-Viewer" +
                        "-${variant.buildType.name.toLowerCase()}" +
                        "-${variant.versionName}" +
                        "-${variant.flavorName.toLowerCase()}" +
                        ".apk")
            }
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url "http://kodbiro.github.io/repo/" }
    maven { url "http://dl.bintray.com/glomadrian/maven" }
    maven { url 'http://guardian.github.com/maven/repo-releases' }
    maven { url "https://raw.github.com/laenger/maven-releases/master/releases" }
    jcenter()
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    annotationProcessor 'com.google.dagger:dagger-compiler:2.0'
    testCompile 'junit:junit:4.12'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    //Google offical
    compile 'com.android.support:support-v4:25.2.0'
    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support:cardview-v7:25.2.0'
    compile 'com.android.support:design:25.2.0'
    compile 'com.android.support:support-compat:25.2.0'
    //umeng analytics
    compile 'com.umeng.analytics:analytics:latest.integration'
    //material widget
    compile 'com.github.vajro:MaterialDesignLibrary:1.6'
    compile 'com.github.opacapp:multiline-collapsingtoolbar:v1.3.1'
    compile 'com.github.rengwuxian:MaterialEditText:2.1.4'
    compile 'com.github.clans:fab:1.6.1'
    compile 'com.balysv:material-ripple:1.0.2'
    compile 'com.github.MiguelCatalan:MaterialSearchView:v1.4.0'
    compile 'com.github.glomadrian:MaterialAnimatedSwitch:1.1@aar'
    compile('com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.10.4@aar') {
        transitive = true
    }
    compile 'com.github.leonardarnold:NumberPickerPreference:bea5996cc9'
    compile 'me.majiajie:pager-bottom-tab-strip:1.0.0'
    compile 'com.github.davidpizarro:autolabelui:1.0.1'
    compile ('com.github.smtchahal:MaterialSpinner:1.1.7'){
        exclude group: 'com.nineoldandroids', module: 'library'
        exclude group: 'com.android.support', module: 'appcompat-v7'
    }
    compile "biz.laenger.android:vpbs:0.0.4"
    //Pull Load More
    compile('com.wuxiaolong.pullloadmorerecyclerview:library:1.1.2') {
        exclude group: 'com.android.support'
    }
    // SwipeBack

    //    compile 'me.majiajie:swipe-back:1.0.0-alpha2'

    //butter knife
    compile 'com.jakewharton:butterknife:8.2.1'
    //image
    compile 'com.facebook.fresco:fresco:0.13.0'
    compile 'com.facebook.fresco:animated-gif:0.13.0'
    compile 'com.facebook.fresco:imagepipeline-okhttp3:0.12.0+'
    compile 'q.rorbin:FastImageSize:1.0.2'
    //    compile 'me.relex:photodraweeview:1.1.0'
    compile 'com.github.PureDark:PhotoDraweeView:2c7b7f65b1'
    //    compile project (':photodraweeview')
    compile 'com.nineoldandroids:library:2.4.0'
    //animation
    compile 'com.daimajia.easing:library:2.0@aar'
    compile 'com.daimajia.androidanimations:library:2.2@aar'

    //http
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.lzy.net:okgo:2.1.4'
    compile 'com.lzy.net:okserver:1.1.3'
    //httpdns
    compile project(':httpdns')
    //email
    compile files('libs/activation.jar')
    compile files('libs/additionnal.jar')
    compile files('libs/mail.jar')
    compile files('libs/commons-email-1.4.jar')
    //format, gson, jsoup, etc
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.github.jhy:jsoup:jsoup-1.9.2'
    compile('com.jayway.jsonpath:json-path:2.2.0') {
        exclude group: 'org.ow2.asm'
    }
    //zxing
    compile 'com.journeyapps:zxing-android-embedded:3.0.2@aar'
    compile 'com.google.zxing:core:3.2.0'
    // directory chooser
    compile 'com.gu:option:1.3'
    compile 'net.rdrei.android.dirchooser:library:3.2@aar'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.2.1'
    compile 'net.jcip:jcip-annotations:1.0'

    // unlock methods, such as pin, fingerprint, etc
    compile 'com.andrognito.pinlockview:pinlockview:2.0.1'
    compile 'com.zwh:RxFingerPrinter:1.0.1'
    compile 'me.zhanghai.android.patternlock:library:2.0.4'
    compile 'com.eightbitlab:blurview:1.3.2'

    //mutidex
    compile 'com.android.support:multidex:+'
}
